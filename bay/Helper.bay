/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2021 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.ORM;

use Runtime.lib;
use Runtime.BaseDriver;
use Runtime.Context;
use Runtime.Driver;
use Runtime.LambdaChain;
use Runtime.LambdaChainClass;
use Runtime.ORM.Annotations.Record;
use Runtime.ORM.Annotations.Table;
use Runtime.ORM.Annotations.TableList;
use Runtime.ORM.StructBuilder;


@LambdaChainClass{}
@Driver{ "name": "Runtime.ORM.Helper" }
class Helper extends BaseDriver
{
	static const string BUILD_TABLE_LIST_STRUCT_CHAIN = "Runtime.ORM.BUILD_TABLE_LIST_STRUCT_CHAIN";
	static const string BUILD_TABLE_STRUCT_CHAIN = "Runtime.ORM.BUILD_TABLE_STRUCT_CHAIN";
	public Dict<StructBuilder> tables = {};
	
	
	
	/**
	 * Start driver
	 */
	public async void startDriver()
	{
		await this.buildStruct();
	}
	
	
	
	/**
	 * Build tables struct
	 */
	public async void buildStruct()
	{
		this.tables = await @.chainAsync(static::BUILD_TABLE_LIST_STRUCT_CHAIN, [ this.tables ]) |> attr 0;
	}
	
	
	
	/**
	 * Build tables
	 */
	@LambdaChain{ "name": static::BUILD_TABLE_LIST_STRUCT_CHAIN }
	static async Dict<StructBuilder> buildTables(Dict<StructBuilder> tables)
	{
		Collection<Table> annotations = @.entities.filter( lib::isInstance( classof Table) );
		
		for (int i=0; i<annotations.count(); i++)
		{
			Table table = annotations[i];
			StructBuilder builder = null;
			
			if (tables.has(table.name))
			{
				builder = tables.get(table.name)
			}
			else
			{
				builder = new StructBuilder
				{
					"object_name": table.name,
					"table_name": table.table_name
				};
			}
			
			/* Extend struct */
			string class_name = table.entityClassName();
			builder = await rtl::methodApplyAsync(class_name, "struct", [builder]);
			
			/* Extend struct */
			builder = await @.chainAsync(static::BUILD_TABLE_STRUCT_CHAIN, [ builder ]) |> attr 0;
			
			/* Add to tables */
			tables = tables.setIm(builder.object_name, builder);
		}
		
		return [ tables ];
	}
}