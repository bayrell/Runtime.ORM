/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.ORM;

use Runtime.BaseObject;
use Runtime.ORM.Provider;


class Record extends BaseObject
{
	Map old_data = null;
	Map new_data = new Map;
	
	
	/**
	 * Create new Record
	 */
	void constructor(Map data)
	{
		parent();
		this._initData(data);
	}
	
	
	/**
	 * Convert to Dict
	 */
	Map all() => this.new_data.copy();
	Map old() => this.old_data.copy();
	Map getData() => this.new_data.copy();
	Map intersect(Collection fields = null) => this.new_data.intersect(fields);
	
	
	/**
	 * Returns primary key
	 */
	public Map getPrimaryKey()
	{
		Provider provider = @.provider(classof Provider);
		return provider.getPrimaryFromData(static::getTableName(), this.new_data);
	}
	
	
	/**
	 * Returns value
	 */
	var get(string name, var def_value = null) => this.new_data.get(name, def_value);
	
	
	/**
	 * Set new value
	 */
	void set(string name, var value)
	{
		this.new_data.set(name, value);
	}
	
	
	/**
	 * Assign data
	 */
	void assign(Map data)
	{
		Vector keys = rtl::list(data.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			this.set(key, data.get(key));
		}
	}
	void setData(Dict item) => this.assign(item);
	
	
	/**
	 * Get updated data
	 */
	Map getUpdatedData()
	{
		if (this.new_data == null) return {};
		
		Map res = new Map;
		
		Collection new_data_keys = rtl::list(this.new_data.keys());
		for (int i=0; i<new_data_keys.count(); i++)
		{
			string field_name = new_data_keys[i];
			var new_value = this.new_data.get(field_name);
			
			if (this.old_data == null)
			{
				res.set(field_name, new_value);
			}
			else
			{
				if (not this.old_data.has(field_name))
				{
					res.set(field_name, new_value);
				}
				else
				{
					var old_value = this.old_data.get(field_name);
					if (new_value != old_value)
					{
						res.set(field_name, new_value);
					}
				}
			}
		}
		
		return res;
	}
	
	
	/**
	 * Init data
	 */
	protected void _initData(Dict data = null)
	{
		this.old_data = data != null ? new Map(data) : null;
		this.new_data = data != null ? new Map(data) : new Map();
	}
	
	
	/**
	 * Returns true if object is new
	 */
	bool isNew()
	{
		return this.old_data ? false : true;
	}
	
	
	/**
	 * Returns true if data has loaded from database
	 */
	bool isUpdate()
	{
		return this.old_data ? true : false;
	}
	
	
	/**
	 * Returns true if model is changed
	 */
	bool isChanged()
	{
		Dict d1 = this.old_data;
		Dict d2 = this.new_data;
		
		if (d1 == null) return true;
		if (d2 == null) return true;
		
		Collection d1_keys = d1.keys();
		Collection d2_keys = d2.keys();
		
		for (int i=0; i<d1_keys.count(); i++)
		{
			string key1 = d1_keys.get(i);
			
			if (not d2.has(key1))
			{
				return true;
			}
			
			var value1 = d1.get(key1);
			var value2 = d2.get(key1);
			
			if (value1 != value2)
			{
				return true;
			}
		}
		
		for (int i=0; i<d2_keys.count(); i++)
		{
			string key2 = d2_keys.get(i);
			
			if (not d1.has(key2))
			{
				return true;
			}
		}
		
		return false;
	}
}