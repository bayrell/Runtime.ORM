/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.ORM;

use Runtime.BaseStruct;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Factory.CursorFactory;
use Runtime.ORM.Cursor;
use Runtime.ORM.Provider;
use Runtime.ORM.Query;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.QueryLog;
use Runtime.ORM.QueryResult;
use Runtime.ORM.Relation;


class Connection
{
	string name = "";
	CursorFactory cursor = null;
	QueryLog log = null;
	
	
	/**
	 * Returns connection
	 */
	static Connection get(string name="default") => static::getConnection(name);
	static Connection getConnection(string name = "default")
	{
		if (name == "") name = "default";
		
		Provider provider = @.provider(classof Provider);
		Connection conn = provider.getConnection(name);
		return conn;
	}
	
	
	/**
	 * Set cursor factory
	 */
	void setCursorFactory(CursorFactory factory)
	{
		this.cursor = factory;
	}
	
	
	/**
	 * Set query log
	 */
	void setQueryLog(QueryLog value)
	{
		this.log = value;
	}
	
	
	/**
	 * Returns query log
	 */
	QueryLog getQueryLog() => this.log;
	
	
	/**
	 * Constructor
	 */
	public void constructor(string name = "")
	{
		this.name = name;
	}
	
	
	/**
	 * Connect
	 */
	async Connection connect()
	{
	}
	
	
	/**
	 * Check is connected
	 */
	bool isConnected() => false;
	
	
	/**
	 * Returns connection name
	 */
	bool getName() => this.name;
	
	
	/**
	 * Create new cursor
	 */
	Cursor createCursor()
	{
		if (not this.cursor) return;
		Cursor cursor = this.cursor.createCursor();
		cursor.setConnection(this);
		return cursor;
	}
	
	
	/**
	 * Fork connection
	 */
	Connection fork()
	{
		return rtl::newInstance(static::getClassName());
	}
	
	
	/**
	 * Prepare field
	 */
	string prepareField(var item) => item;
	
	
	/**
	 * Prepare value
	 */
	string prepareValue(string item, string op) => item;
	
	
	/**
	 * Quote
	 */
	string quote(string value) => value;
	
	
	/**
	 * Returns table name
	 */
	string getTableName(string table_name) => this.prefix ~ table_name;
	
	
	/**
	 * Execute Query
	 */
	async Cursor execute(Query q, Dict params = null) => null;
	
	
	/**
	 * Insert query
	 */
	async var insert(
		string table_name,
		Dict insert_data,
		bool get_last_id = true,
		Dict params = null
	)
	{
		var last_id = null;
		
		if (table_name == "")
		{
			throw new RuntimeException("Table name is empty");
		}
		
		Query q = new Query()
			.insert(table_name)
			.values(insert_data)
		;
		
		Cursor c = await this.execute(q, params);
		
		if (get_last_id)
		{
			last_id = await c.lastInsertId();
		}
		
		await c.close();
		
		return last_id;
	}
	
	
	/**
	 * Update query
	 */
	async void update(
		string table_name,
		Collection filter,
		Dict update_data,
		Dict params = null
	)
	{
		if (table_name == "")
		{
			throw new RuntimeException("Table name is empty");
		}
		
		Query q = new Query()
			.update(table_name)
			.values(update_data)
			.setFilter(filter)
		;
		
		Cursor c = await this.execute(q, params);
		await c.close();
	}
	
	
	/**
	 * Delete item
	 */
	async void delete(
		string table_name,
		Collection filter,
		Dict params = null
	)
	{
		if (table_name == "")
		{
			throw new RuntimeException("Table name is empty");
		}
		
		Query q = new Query()
			|> .delete(table_name)
			|> .setFilter(filter)
		;
		
		Cursor c = await this.execute(q, params);
		await c.close();
	}
	
	
	/**
	 * Convert item from database
	 */
	var fromDatabase(BaseStruct annotation, Dict item, string field_name) => item;
	
	
	/**
	 * Convert item to database
	 */
	var toDatabase(BaseStruct annotation, Dict item, string field_name) => item;
	
	
	/**
	 * Fetch all
	 */
	async QueryResult fetchAll(Query q, Dict params = null)
	{
		Cursor c = await this.execute(q, params);
		QueryResult items = await c.fetchAll();
		await c.close();
		return items;
	}
	
	
	/**
	 * Fetch
	 */
	async QueryResult fetch(Query q, Dict params = null)
	{
		Cursor c = await this.execute(q, params);
		QueryResult items = await c.fetch();
		await c.close();
		return items;
	}
	async Dict fetchOne(Query q, Dict params = null) => await this.fetch(q, params);
	
	
	/**
	 * Fetch variable
	 */
	async var fetchVar(Query q, string var_name, Dict params = null)
	{
		Cursor cursor = await this.execute(q, params);
		var item = await cursor.fetchVar(var_name);
		await cursor.close();
		return item;
	}
	
	
	/**
	 * Find relation by query
	 */
	async Relation findRelation(Query q, Dict params = null)
	{
		Cursor c = await this.execute(q, params);
		Relation item = await c.fetchRelation(q, params);
		await c.close();
		return item;
	}
	
	
	/**
	 * Find relations by query
	 */
	async Relation findRelations(Query q, Dict params = null)
	{
		Cursor c = await this.execute(q, params);
		QueryResult items = await c.fetchAll(q, params);
		await c.close();
		return items.toRelation();
	}
}