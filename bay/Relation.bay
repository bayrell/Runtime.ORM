/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.ORM;

use Runtime.lib;
use Runtime.BaseObject;
use Runtime.BaseStruct;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.DatabaseSchema;
use Runtime.ORM.Provider;
use Runtime.ORM.Query;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.Annotations.AutoIncrement;
use Runtime.ORM.Annotations.BaseType;
use Runtime.ORM.Annotations.ForeignKey;
use Runtime.ORM.Annotations.Primary;


class Relation extends BaseObject
{
	protected string table_name = "";
	protected Map old_data = null;
	protected Map new_data = new Map;
	protected bool is_dirty = false;
	
	
	/**
	 * Constructor
	 */
	void constructor(string table_name)
	{
		parent();
		
		if (table_name == "")
		{
			throw new RuntimeException("Table name is empty");
		}
		this.table_name = table_name;
	}
	
	
	/**
	 * Returns table name
	 */
	string getTableName() => this.table_name;
	
	
	/**
	 * Create Instance of class
	 */
	pure Relation newInstance(string table_name, Dict data = null)
	{
		Relation instance = rtl::newInstance(static::getClassName(), [table_name]);
		instance._setNewData(data);
		return instance;
	}
	
	
	/**
	 * Create Instance of class from database
	 */
	pure Relation newInstanceFromDatabase(string table_name, Connection conn, Dict data = null)
	{
		data = static::fromDatabase(table_name, conn, data);
		data = static::newInstance(table_name, data);
		return data;
	}
	
	
	/**
	 * Convert to model
	 */
	BaseStruct toModel(string class_name) =>
		rtl::newInstance(class_name, [ this.toDict() ])
	;
	
	
	/**
	 * Convert to Dict
	 */
	Dict toDict() => this.new_data.toDict();
	Dict data() => this.new_data.toDict();
	
	
	/**
	 * Returns table annotations
	 */
	pure memorize Collection<BaseStruct> getAnotations(string table_name)
	{
		Provider provider = @.provider(classof Provider);
		return provider.getAnotations(table_name);
	}
	
	
	/**
	 * Returns foreign_key
	 */
	pure memorize ForeignKey getForeignKey(string name)
	{
		Provider provider = @.provider(classof Provider);
		return provider.getForeignKey(name);
	}
	
	
	/**
	 * To database
	 */
	static Dict toDatabase(string table_name, Connection conn, Dict data, bool is_update)
	{
		if (data == null) return null;
		
		Collection<BaseStruct> annotations = static::getAnotations(table_name);
		for (int i=0; i<annotations.count(); i++)
		{
			BaseStruct annotation = annotations[i];
			if (annotation instanceof BaseType)
			{
				data = annotation.toDatabase(conn, data, is_update);
			}
		}
		return data;
	}
	
	
	/**
	 * From database
	 */
	static Dict fromDatabase(string table_name, Connection conn, Dict data)
	{
		if (data == null) return null;
		
		Collection<BaseStruct> annotations = static::getAnotations(table_name);
		for (int i=0; i<annotations.count(); i++)
		{
			BaseStruct annotation = annotations[i];
			if (annotation instanceof BaseType)
			{
				data = annotation.fromDatabase(conn, data);
			}
		}
		return data;
	}
	
	
	/**
	 * Returns true if primary key is auto increment
	 */
	pure memorize AutoIncrement getAutoIncrement(string table_name)
	{
		Collection<BaseStruct> annotations = static::getAnotations(table_name);
		for (int i=0; i<annotations.count(); i++)
		{
			BaseStruct annotation = annotations[i];
			if (annotation instanceof AutoIncrement)
			{
				return annotation;
			}
		}
		return null;
	}
	
	
	/**
	 * Returns primary keys of table
	 */
	pure memorize Collection<string> getPrimaryKeys(string table_name)
	{
		Vector primary_keys = new Vector();
		Collection<BaseStruct> annotations = static::getAnotations(table_name);
		for (int i=0; i<annotations.count(); i++)
		{
			BaseStruct annotation = annotations[i];
			if (annotation instanceof Primary)
			{
				Collection keys = annotation.keys;
				for (int j=0; j<keys.count(); j++)
				{
					string field_name = keys[j];
					primary_keys.pushValue(field_name);
				}
			}
		}
		return primary_keys.toCollection();
	}
	
	
	/**
	 * Returns primary data
	 */
	pure Dict getPrimaryFromData(string table_name, Dict data)
	{
		Map primary_data = new Map();
		Collection<BaseStruct> annotations = static::getAnotations(table_name);
		for (int i=0; i<annotations.count(); i++)
		{
			BaseStruct annotation = annotations[i];
			if (annotation instanceof Primary)
			{
				Collection<string> keys = annotation.keys;
				for (int j=0; j<keys.count(); j++)
				{
					string field_name = keys[j];
					primary_data.setValue(field_name, data[field_name]);
				}
			}
		}
		return primary_data.toDict();
	}
	
	
	/**
	 * Returns primary filter by data
	 */
	pure Collection<QueryFilter> getPrimaryFilter(string table_name, Dict data, bool use_alias)
	{
		Vector filter = new Vector();
		
		Dict pk = static::getPrimaryFromData(table_name, data);
		pk.each(
			void (var value, string key) use (filter, use_alias)
			{
				QueryFilter item = new QueryFilter;
				item.key = key;
				item.op = "=";
				item.value = value;
				if (use_alias)
				{
					item.key = "t." ~ key;
				}
				filter.pushValue(item);
			}
		);
		
		return filter.toCollection();
	}
	
	
	/**
	 * Returns primary key
	 */
	public Dict getPrimaryKey()
	{
		return static::getPrimaryFromData(this.table_name, this.new_data);
	}
	
	
	/**
	 * Returns primary key
	 */
	public Dict pk()
	{
		return static::getPrimaryFromData(this.table_name, this.new_data);
	}
	
	
	/**
	 * Get updated data
	 */
	Dict getUpdatedData()
	{
		if (this.new_data == null) return {};
		
		Map res = new Map;
		
		Collection new_data_keys = this.new_data.keys();
		for (int i=0; i<new_data_keys.count(); i++)
		{
			string field_name = new_data_keys[i];
			var new_value = this.new_data[field_name];
			
			if (this.old_data == null)
			{
				res.setValue(field_name, new_value);
			}
			else
			{
				if (not this.old_data.has(field_name))
				{
					res.setValue(field_name, new_value);
				}
				else
				{
					var old_value = this.old_data[field_name];
					if (new_value != old_value)
					{
						res.setValue(field_name, new_value);
					}
				}
			}
		}
		
		return res.toDict();
	}
	
	
	/**
	 * Set new data
	 */
	protected void _setNewData(Dict data = null)
	{
		this.is_dirty = false;
		this.old_data = data != null ? new Map(data) : null;
		this.new_data = data != null ? new Map(data) : new Map();
	}
	
	
	/**
	 * Returns true if object is new
	 */
	bool isNew()
	{
		return this.old_data ? false : true;
	}
	
	
	/**
	 * Returns true if data has loaded from database
	 */
	bool isUpdate()
	{
		return this.old_data ? true : false;
	}
	
	
	/**
	 * Returns true if model is changed
	 */
	bool isDirty()
	{
		return this.is_dirty;
	}
	
	
	/**
	 * Find item
	 */
	async static Relation findById
		(string table_name, Connection conn, string id, Dict params = null)
	{
		Collection pk = static::getPrimaryKeys(table_name);
		string pk_name = pk.get(0);
		if (not pk_name) return null;
		
		Query q = new Query
			|> .select(table_name)
			|> .where(pk_name, "=", id)
		;
		
		Relation item = await conn.fetchOne(q, params);
		return item;
	}
	
	
	/**
	 * Find item
	 */
	async static Relation findItem
		(string table_name, Connection conn, Dict item, Dict params = null)
	{
		Vector filter = new Vector();
		Collection keys = item.keys();
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys[i];
			filter.pushValue(["t." ~ key, "=", item[key]]);
		}
		
		Query q = new Query
			|> .select(table_name)
			|> .setFilter(filter)
			|> .limit(1)
		;
		
		Relation item = await conn.fetchOne(q, params);
		return item;
	}
	
	
	/**
	 * Find items
	 */
	async static Collection<Relation> findItems
		(string table_name, Connection conn, Collection filter, Dict params = null)
	{
		Query q = new Query
			|> .select(table_name)
			|> .setFilter(filter)
		;
		
		Collection<Relation> items = await conn.fetchAll(q, params);
		return items;
	}
	
	
	/**
	 * Find or create
	 */
	async static Relation findOrCreate
		(string table_name, Connection conn, Dict item, Dict params = null)
	{
		/* Find item */
		Relation res = await static::findItem(table_name, conn, item, params);
		if (res) return res;
		
		/* Create if not found */
		res = static::newInstance(table_name, item);
		return res;
	}
	
	
	/**
	 * Update items
	 */
	static async void updateItems(
		string table_name,
		Connection conn, Collection filter,
		Dict update_data, Dict params = null
	)
	{
		Query q = new Query
			|> .update(table_name)
			|> .values(update_data)
			|> .setFilter(filter)
		;
		Cursor c = await conn.executeQuery(q, params);
		await c.close();
	}
	
	
	/**
	 * Delete items
	 */
	static async void deleteItems(
		string table_name, Connection conn, Collection filter, Dict params = null
	)
	{
		Query q = new Query
			|> .delete(table_name)
			|> .setFilter(filter)
		;
		
		Cursor c = await conn.executeQuery(q, params);
		await c.close();
	}
	
	
	/**
	 * Save model
	 */
	async void save(Connection conn, Dict params = null)
	{
		/* Call before save */
		await @.callAsyncHook(DatabaseSchema::SAVE_BEFORE, {
			"item": this,
		});
		
		bool is_update = this.isUpdate();
		Dict updated_data = this.getUpdatedData();
		
		if (is_update)
		{
			Collection updated_data_keys = updated_data.keys();
			if (updated_data_keys.count() > 0)
			{
				Collection filter = static::getPrimaryFilter(this.table_name, this.old_data, false);
				if (filter.count() > 0)
				{
					Dict db_updated_data = static::toDatabase
						(this.table_name, conn, updated_data, is_update);
					
					await conn.update(
						this.table_name,
						filter,
						db_updated_data,
						params
					);
				}
				else
				{
					throw new RuntimeException("Primary key does not exists in " ~ this.table_name);
				}
				
				for (int i=0; i<updated_data_keys.count(); i++)
				{
					string field_name = updated_data_keys[i];
					this.old_data.setValue(field_name, updated_data[field_name]);
				}
			}
		}
		else
		{
			Dict db_updated_data = static::toDatabase
				(this.table_name, conn, updated_data, is_update);
			
			var last_id = await conn.insert(
				this.table_name,
				db_updated_data,
				true,
				params
			);
			
			this._setNewData(this.new_data);
			
			AutoIncrement auto_increment = static::getAutoIncrement(this.table_name);
			if (auto_increment and auto_increment.name)
			{
				this.old_data.setValue(auto_increment.name, last_id);
				this.new_data.setValue(auto_increment.name, last_id);
			}
		}
		
		/* Call before after */
		await @.callAsyncHook(DatabaseSchema::SAVE_AFTER, {
			"item": this,
		});
	}
	
	
	/**
	 * Delete model
	 */
	async Relation delete(Connection conn, Dict params = null)
	{
		Collection filter = static::getPrimaryFilter(this.table_name, this.old_data, false);
		
		if (filter.count() > 0)
		{
			await conn.delete(
				this.table_name,
				filter,
				params
			);
		}
		else
		{
			throw new RuntimeException("Primary key does not exists in " ~ this.table_name);
		}
		
		return this;
	}
	
	
	/**
	 * Refresh model from database
	 */
	async Relation refresh(Connection conn, Dict params = null)
	{
		Dict item = null;
		Collection<QueryFilter> filter = static::getPrimaryFilter(this.table_name, this.old_data, false);
		
		if (filter.count() > 0)
		{
			Query q = new Query
				|> .select(this.table_name)
				|> .setFilter(filter)
				|> .limit(1)
			;
			
			item = await conn.fetchOne(q, params);
		}
		
		if (item)
		{
			this._setNewData(item.toDict());
		}
		
		return this;
	}
	
	
	/**
	 * Returns value
	 */
	var get(string name, var def_value = null) => this.new_data[name] |> default def_value;
	
	
	/**
	 * Set new value
	 */
	void set(string name, var value)
	{
		this.new_data.setValue(name, value);
	}
}