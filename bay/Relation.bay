/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.ORM;

use Runtime.lib;
use Runtime.BaseObject;
use Runtime.BaseStruct;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.DatabaseSchema;
use Runtime.ORM.Provider;
use Runtime.ORM.Query;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.QueryResult;
use Runtime.ORM.Record;
use Runtime.ORM.Annotations.AutoIncrement;
use Runtime.ORM.Annotations.BaseType;
use Runtime.ORM.Annotations.ForeignKey;
use Runtime.ORM.Annotations.Primary;


class Relation extends BaseObject
{
	protected Connection connection = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(Dict params = null)
	{
		parent();
		if (params.has("connection")) this.connection = params.get("connection");
		else if (params.has("connection_name"))
		{
			Provider provider = @.provider(classof Provider);
			this.connection = provider.get(params.get("connection_name"));
		}
	}
	
	
	/**
	 * Returns table name
	 */
	pure string getTableName() => "";
	
	
	/**
	 * Returns connection;
	 */
	Connection getConnection() => this.connection;
	
	
	/**
	 * Set connection
	 */
	void setConnection(Connection connection)
	{
		this.connection = connection;
	}
	
	
	/**
	 * Create Instance of class
	 */
	static Record createRecord(Dict data = null)
	{
		Record instance = new Record(static::getTableName());
		instance._initData(data);
		return instance;
	}
	
	
	/**
	 * Returns table annotations
	 */
	Collection<BaseStruct> getAnotations()
	{
		Provider provider = @.provider(classof Provider);
		return provider.getAnotations(static::getTableName());
	}
	
	
	/**
	 * To database
	 */
	async Dict toDatabase(Dict data, bool is_update)
	{
		if (data == null) return null;
		
		Collection<BaseType> annotations = this.getAnotations();
		for (int i=0; i<annotations.count(); i++)
		{
			BaseType annotation = annotations[i];
			if (annotation instanceof BaseType)
			{
				data = await annotation.toDatabase(this.connection, data, is_update);
			}
		}
		
		return data;
	}
	
	
	/**
	 * From database
	 */
	Dict fromDatabase(Connection conn, Dict data)
	{
		if (data == null) return null;
		
		Collection<BaseType> annotations = this.getAnotations();
		for (int i=0; i<annotations.count(); i++)
		{
			BaseType annotation = annotations[i];
			if (annotation instanceof BaseType)
			{
				data = await annotation.fromDatabase(this.connection, data);
			}
		}
		
		return data;
	}
	
	
	/**
	 * Returns true if primary key is auto increment
	 */
	AutoIncrement getAutoIncrement()
	{
		Collection<BaseObject> annotations = this.getAnotations();
		AutoIncrement annotation = annotations.find(
			bool (BaseObject item) => item instanceof AutoIncrement
		);
		return annotation;
	}
	
	
	/**
	 * Returns primary keys of table
	 */
	Collection<string> getPrimaryKeys()
	{
		/* Get primary annotation */
		Collection<BaseStruct> annotations = this.getAnotations();
		Primary primary = annotations.find(bool (BaseObject item) => item instanceof Primary);
		
		if (primary == null) return null;
		return primary.keys;
	}
	
	
	/**
	 * Returns primary data
	 */
	Dict getPrimaryKey(Dict data)
	{
		/* Get primary annotation */
		Collection<string> primary_keys = this.getPrimaryKeys();
		
		/* Check if primary keys if exists */
		if (primary_keys == null)
		{
			throw new ItemNotFound("Primary keys");
		}
		
		/* Intersect values */
		return data ? data.intersect(primary_keys) : {};
	}
	
	
	/**
	 * Returns primary filter by data
	 */
	Vector<QueryFilter> getPrimaryFilter(Dict data, bool use_full_key = true)
	{
		string table_name = static::getTableName();
		Dict pk = this.getPrimaryKey(data);
		Vector filter = pk.transition(
			QueryFilter (var value, string key) use (table_name, use_full_key)
			{
				QueryFilter item = new QueryFilter;
				item.key = key;
				item.op = "=";
				item.value = value;
				if (use_full_key)
				{
					item.key = table_name ~ "." ~ key;
				}
				return item;
			}
		);
		return filter;
	}
	
	
	/**
	 * Save model
	 */
	async void save(Record item, Dict params = null)
	{
		string table_name = static::getTableName();
		
		/* Call before save */
		await @.hook(DatabaseSchema::SAVE_BEFORE, {
			"table_name": table_name,
			"relation": this,
			"item": item,
		});
		
		bool is_update = item.isUpdate();
		Dict updated_data = item.getUpdatedData();
		
		if (is_update)
		{
			Collection updated_data_keys = rtl::list(updated_data.keys());
			if (updated_data_keys.count() > 0)
			{
				Collection filter = this.getPrimaryFilter(item.old_data, false);
				if (filter.count() > 0)
				{
					Dict db_updated_data = await this.toDatabase(updated_data, is_update);
					
					await this.connection.update(
						table_name,
						filter,
						db_updated_data,
						params
					);
				}
				else
				{
					throw new RuntimeException("Primary key does not exists in " ~ table_name);
				}
				
				for (int i=0; i<updated_data_keys.count(); i++)
				{
					string field_name = updated_data_keys[i];
					item.old_data.set(field_name, updated_data.get(field_name));
				}
			}
		}
		else
		{
			Dict db_updated_data = this.toDatabase(updated_data, is_update);
			
			var last_id = await this.connection.insert(
				table_name,
				db_updated_data,
				true,
				params
			);
			
			item._initData(item.new_data);
			
			AutoIncrement auto_increment = this.getAutoIncrement(table_name);
			if (auto_increment and auto_increment.name)
			{
				item.old_data.set(auto_increment.name, last_id);
				item.new_data.set(auto_increment.name, last_id);
			}
		}
		
		/* Call before after */
		await @.hook(DatabaseSchema::SAVE_AFTER, {
			"table_name": table_name,
			"relation": this,
			"item": item,
		});
	}
	
	
	/**
	 * Delete model
	 */
	async void delete(Record item, Dict params = null)
	{
		if (item.isNew())
		{
			return;
		}
		
		/* Get primary filter */
		string table_name = static::getTableName();
		Collection filter = this.getPrimaryFilter(item.old_data, false);
		
		/* Delete record */
		if (filter.count() > 0)
		{
			await this.connection.delete(
				table_name,
				filter,
				params
			);
		}
		else
		{
			throw new RuntimeException("Primary key does not exists in " ~ table_name);
		}
	}
	
	
	/**
	 * Refresh model from database
	 */
	async Record refresh(Record item, Dict params = null)
	{
		item = this.findByPk(item, params);
		if (item)
		{
			item._initData(item.toDict());
		}
		return item;
	}
	
	
	/**
	 * Returns query
	 */
	Query query() => new Query().from(static::getTableName());
	
	
	/**
	 * Returns select query
	 */
	Query select()
	{
		string table_name = static::getTableName();
		Query q = new Query()
			.select([table_name ~ ".*"])
			.from(table_name)
		;
		return q;
	}
	
	
	/**
	 * Fetch all
	 */
	async QueryResult fetchAll(Query q, Dict params = null)
	{
		Cursor c = await this.connection.execute(q, params);
		QueryResult items = await c.fetchAll();
		await c.close();
		return items;
	}
	
	
	/**
	 * Fetch
	 */
	async Record fetch(Query q, Dict params = null)
	{
		Cursor c = await this.connection.execute(q, params);
		Record item = await c.fetch();
		await c.close();
		return item;
	}
	async Record fetchOne(Query q, Dict params = null) =>
		await this.fetch(q, params)
	;
	
	
	/**
	 * Find relations by filter
	 */
	async QueryResult findByFilter(Collection<QueryFilter> filter, Dict params = null)
	{
		Query q = this.select().setFilter(filter);
		return await this.connection.fetchAll(q, params);
	}
	
	
	/**
	 * Find relation by filter
	 */
	async Record findItem(Collection<QueryFilter> filter, Dict params = null)
	{
		Query q = this.select().setFilter(filter).limit(1);
		return await this.connection.fetch(q, params);
	}
	
	
	/**
	 * Find relation by primary key
	 */
	async Record findByPk(Dict pk, Dict params = null)
	{
		/* Returns primary filter */
		Vector filter = this.getPrimaryFilter(pk, false);
		
		/* Find record */
		return await this.findItem(filter, params);
	}
	
	
	/**
	 * Find relation by object
	 */
	async Record findById(var id, Dict params = null)
	{
		/* Get primary keys */
		Vector pk = this.getPrimaryKeyNames();
		string pk_name = pk.get(0);
		
		/* Build filter */
		Vector filter = [
			new QueryFilter(pk_name, "=", id)
		];
		
		/* Find record */
		return await this.findItem(filter, params);
	}
	
	
	/**
	 * Find or create
	 */
	async Record findOrCreate(Dict filter, Dict update, Dict params = null)
	{
		string class_name = static::getClassName();
		
		/* Build filter */
		Vector search = filter.transition(
			QueryFilter (var value, string key)
			{
				return new QueryFilter(key, "=", value);
			}
		);
		
		/* Find item */
		Record record = await this.findItem(search, params);
		if (record) return record;
		
		/* Create if not found */
		record = Record::newInstance(class_name);
		
		/* Set new data */
		record.setData(filter);
		record.setData(update);
		
		return record;
	}
}